<S>                 -> <program> $ 
<program>           -> begin <statementlist> end 
<statementlist>     -> <statement> <statementtail> 
<statementtail>     -> <statement> <statementtail> 
<statementtail>     -> epsilon 
<statement>         -> ID := <expression> ; 
<statement>         -> read ( <idlist> ) ; 
<statement>         -> write ( <exprlist> ) ; 
<idlist>            -> ID <idtail> 
<idtail>            -> , ID <idtail> 
<idtail>            -> epsilon 
<exprlist>          -> <expression> <exprtail> 
<exprtail>          -> , <expression> <exprtail> 
<exprtail>          -> epsilon 
<expression>        -> <primary> <primarytail> 
<primarytail>       -> <addop> <primary> <primarytail> 
<primarytail>       -> epsilon 
<primary>           -> ( <expression> ) 
<primary>           -> ID 
<primary>           -> INTLIT 
<addop>             -> + 
<addop>             -> - 
