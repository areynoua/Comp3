<Program>           -> begin <Code> end 
<Code>              -> epsilon 
                    -> <InstList> 
<InstList>          -> <Instruction> 
                    -> <Instruction> ; <InstList> 
<Instruction>       -> <Assign> 
                    -> <If> 
                    -> <While> 
                    -> <For> 
                    -> <Print> 
                    -> <Read> 
                    -> <Rand> 
                    -> <Define> 
                    -> <Call> 
<Define>            -> function [FuncName] ( [VarName] ) do <Code> end 
<Call>              -> [FuncName] ( [VarName] ) 
<Assign>            -> [VarName] := <ExprArith> 
<ExprArith>         -> [VarName] 
                    -> [Number] 
                    -> ( <ExprArith> ) 
                    -> - <ExprArith> 
                    -> <ExprArith> <Op> <ExprArith> 
<Op>                -> + 
                    -> - 
                    -> * 
                    -> / 
<If>                -> if <Cond> then <Code> endif 
                    -> if <Cond> then <Code> else <Code> endif 
<Cond>              -> <Cond> <BinOp> <Cond> 
                    -> not <SimpleCond> 
                    -> <SimpleCond> 
<SimpleCond>        -> <ExprArith> <Comp> <ExprArith> 
<BinOp>             -> and 
                    -> or 
<Comp>              -> = 
                    -> >= 
                    -> > 
                    -> <= 
                    -> < 
                    -> <> 
<While>             -> while <Cond> do <Code> done 
<For>               -> for [VarName] from <ExprArith> by <ExprArith> to <ExprArith> do <Code> done 
                    -> for [VarName] from <ExprArith> to <ExprArith> do <Code> done 
<Print>             -> print ( [VarName] ) 
<Read>              -> read ( [VarName] ) 
<Rand>              -> rand ( [VarName] ) 
